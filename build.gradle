/*
 * build.gradle
 *
 * Copyright (c) 2011-2014, Daniel Ellermann
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */


import org.apache.tools.ant.filters.ReplaceTokens
import org.gradle.api.JavaVersion


// build script properties
ext {
    groovyVersion = '2.3.6'
    tomcatVersion = '7.0.54'
    springcrmDir = '../springcrm'
    springcrmWar = "${springcrmDir}/target/springcrm.war"
}

// plugins
apply plugin: 'eclipse'
apply plugin: 'groovy'

// pre-defined properties
version = getAppVersion()
targetCompatibility = JavaVersion.VERSION_1_6

// repositories
repositories {
    mavenCentral()
}

// dependencies
dependencies {
    compile 'org.apache.logging.log4j:log4j-core:2.1'
    compile "org.apache.tomcat:tomcat-catalina:${tomcatVersion}"
    compile "org.apache.tomcat:tomcat-coyote:${tomcatVersion}"
    compile "org.apache.tomcat:tomcat-servlet-api:${tomcatVersion}"
    compile "org.codehaus.groovy:groovy-all:${groovyVersion}"
}

// task type configurations
jar {
    manifest {
        attributes 'Implementation-Title': 'SpringCRM standalone',
            'Implementation-Version': version,
            'Main-Class': 'org.amcworld.springcrm.launcher.Launcher'
    }
    baseName = 'springcrm-standalone'
    from {
        configurations.compile.collect { it.directory ? it : zipTree(it) }
    }
    from(springcrmWar) { rename { 'embedded.war' } }
}
processResources.from(sourceSets.main.resources, {
        exclude '**/*.properties'
    }).from(sourceSets.main.resources, {
        include '**/*.properties'
        filter ReplaceTokens, tokens: [
            version: version, copyrightYear: new Date().format('yyyy')
        ]
    })

// auxiliary functions
String getAppVersion() {
    def stream
    try {
        def props = new Properties()
        stream = new FileInputStream("${springcrmDir}/application.properties")
        props.load stream
        props.get('app.version')
    } finally {
        stream?.close()
    }
}

// vim:set ft=groovy:

